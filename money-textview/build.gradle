apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = '0.9.5'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName '0.9.5'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

group = 'net.cpacm.moneytext'

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId 'net.cpacm.moneytext'
            artifactId 'money-textview'

            name 'moneytextview'
            description 'An extended TextView to be used for displaying Money Values.'
            url 'https://github.com/cpacm/MoneyTextView'
            inceptionYear '2016'

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/cpacm/MoneyTextView.git'
                url 'https://github.com/cpacm/MoneyTextView'

            }
            developers {
                developer {
                    name 'cpacm'
                    email 'shenliming@gmail.com'
                }
            }
        }

    }
}

// Bintray
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publish = true
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'moneytextview'
        vcsUrl = 'https://github.com/cpacm/MoneyTextView.git'
        websiteUrl = 'https://github.com/cpacm/MoneyTextView'
        licenses = ['Apache-2.0']
        issueTrackerUrl = 'https://github.com/cpacm/MoneyTextView/issues'
        publicDownloadNumbers = true
        version {
            name = '0.9.5'
            desc = 'money-textview 0.9 release'
            vcsTag = '0.9.5'
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task findConventions << {
    println project.getConvention()
}
